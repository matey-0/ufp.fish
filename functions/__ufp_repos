function update_repos
    # 1. Check if the configuration variable exists
    if not set -q REPO_PATH
        echo "It looks like you haven't set your repository directory yet."
        read -p 'echo "Enter the full path to your repos (e.g. ~/Development/Repos): "' input_path
        
        # Expand the tilde if the user used one
        set -U REPO_PATH (eval echo $input_path)
        
        echo "Your repos are set as being here: $REPO_PATH"
        echo "You can manually edit this later in ~/.config/fish/fish_variables"
        echo ""
    end

    for d in $REPO_PATH/*/
        if test -d $d/.git
            set_color blue; echo "Checking "(basename $d)"..." ; set_color normal

            if not git -C $d diff-index --quiet HEAD --
                set_color yellow; echo "Uncommitted changes found. Skipping pull."; set_color normal
            else
                git -C $d pull
            end
            echo ""
        end
    end
end
